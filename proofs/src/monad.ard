\import Paths
\import list
\import monoid
\import funs
\import sets

\record TypeConstr (F : \Type  -> \Type)

\class Functor \extends TypeConstr {
  | map : \Pi {A B : \Type} -> F A -> (A -> B)  -> F B
  | identity : \Pi { A : \Type } (fa : F A) -> map fa (\lam x => x) = fa
  | composition : \Pi { A B C : \Type } (fa : F A) (f : A -> B) (g : B -> C) -> map fa (g ~ f) = map (map fa f) g
}

\record BaseMonad \extends TypeConstr {
  | pure : \Pi {A : \Type } -> A -> F A
  | flatMap : \Pi {A B : \Type } -> F (A) -> (A -> F B) -> F B
  | left_unit : \Pi { A B : \Type } (a : A) (f : A -> F B) -> flatMap (pure a) f = f a
  | right_unit : \Pi { A : \Type } (fa : F A) -> flatMap fa pure = fa
  | associativity : \Pi { A B C : \Type} (fa : F A) (f : A -> F B) (g : B -> F C) ->
      flatMap fa (\lam x => flatMap (f x) g) = flatMap ( flatMap fa f ) g
}

\class Monad \extends BaseMonad , Functor {
  | map fa f => flatMap  fa (\lam x => pure (f x))
  | identity => right_unit
  | composition {_} {_} {C}  fa f g =>
    \let | gp x => pure (g x)
         | lu_extended => funExt (\lam _ => F C) gp (\lam b => flatMap (pure b) gp) (\lam b => inv ( left_unit b gp))
    \in pmap (\lam u => flatMap fa (u ~ f)) lu_extended *>  associativity fa (pure ~ f) gp
}


